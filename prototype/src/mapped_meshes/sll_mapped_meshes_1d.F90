module sll_module_mapped_meshes_1d
#include "sll_working_precision.h"
#include "sll_memory.h"
#include "sll_assert.h"
  use sll_splines
#ifdef STDF95
  ! We need sll_io because the function write_mesh is write
  ! in the subclass in the F95 case
  ! (it's define in the abstract class in the 2003 case)
  use sll_io
#else 
  use sll_module_interpolators_1d_base
  use sll_module_mapped_meshes_1d_base
#endif
  use sll_cubic_spline_interpolator_1d
  implicit none

  ! A 1D map is specified by the coordinate transformation from eta1 to x1:
  !
  !                x1 = x1(eta1)
  !
  ! Where eta1 is in the interval [0,1]. The same transformation can be 
  ! specified by a set of transformed points x1(i), as a 1D array.
  !
  ! The transformation is also represented by the jacobian :
  !
  !                               partial x1(eta1)  
  !          jacobian(eta1) =   --------------------
  !                               partial   eta1        
  !
  ! For convenience, we can have this pre-evaluated at a collection of 
  ! locations.

#ifdef STDF95
  type                                   :: sll_mapped_mesh_1d_analytic
     sll_int32   :: nc_eta1
     sll_real64  :: delta_eta1
     character(len=64) :: label
     logical           :: written = .false.
#else
  type, extends(sll_mapped_mesh_1d_base) :: sll_mapped_mesh_1d_analytic
#endif
     sll_real64, dimension(:), pointer :: x1_node   ! x1(i) 
     sll_real64, dimension(:), pointer :: x1_cell
     sll_real64, dimension(:), pointer :: jacobians_n
     sll_real64, dimension(:), pointer :: jacobians_c
#ifdef STDF95
#else
     procedure(one_arg_scalar_function), pointer, nopass    :: x1_func  ! user
     procedure(one_arg_scalar_function), pointer, nopass :: &
          jacobian_func
   contains
     procedure, pass(mesh) :: initialize => initialize_mesh_1d_analytic
     procedure, pass(mesh) :: x1_at_node => x1_node_analytic_1d
     procedure, pass(mesh) :: x1_at_cell => x1_cell_analytic_1d
     procedure, pass(mesh) :: jacobian_at_node => mesh_1d_jacobian_node_analytic
     procedure, pass(mesh) :: jacobian_at_cell => mesh_1d_jacobian_cell_analytic
     procedure, pass(mesh) :: x1         => x1_analytic_1d
     procedure, pass(mesh) :: jacobian   => jacobian_1d_analytic
#endif
  end type sll_mapped_mesh_1d_analytic

#ifdef STDF95
  type                                  ::sll_mapped_mesh_1d_discrete
     sll_int32   :: nc_eta1
     sll_real64  :: delta_eta1
     character(len=64) :: label
     logical           :: written = .false.
#else
  type, extends(sll_mapped_mesh_1d_base)::sll_mapped_mesh_1d_discrete
#endif
     sll_real64, dimension(:), pointer                    :: x1_node   ! x1(i)
     sll_real64, dimension(:), pointer                    :: x1_cell
     sll_real64, dimension(:), pointer                    :: jacobians_n
     sll_real64, dimension(:), pointer                    :: jacobians_c
#ifdef STDF95
     type(cubic_spline_1d_interpolator), pointer          :: x1_interp
#else
     class(sll_interpolator_1d_base), pointer             :: x1_interp
     procedure(one_arg_scalar_function), pointer, nopass  :: x1_func
    ! Samuel : this functions seems never initialize, see jacobian
    ! procedure(one_arg_message_passing_func_discr),pointer,pass :: jacobian_func
   contains
     procedure, pass(mesh) :: initialize => initialize_mesh_1d_discrete
     procedure, pass(mesh) :: x1_at_node => x1_node_discrete_1d
     procedure, pass(mesh) :: x1_at_cell => x1_cell_discrete_1d
     procedure, pass(mesh) :: jacobian_at_node => mesh_1d_jacobian_node_discrete
     procedure, pass(mesh) :: jacobian_at_cell => mesh_1d_jacobian_cell_discrete
     procedure, pass(mesh) :: x1         => x1_discrete_1d
     procedure, pass(mesh) :: jacobian   => jacobian_1d_discrete
#endif
  end type sll_mapped_mesh_1d_discrete

#ifdef STDF95
 interface initialize
    module procedure initialize_mesh_1d_analytic, initialize_mesh_1d_discrete
  end interface

  interface write_to_file
    module procedure mma_write_to_file, mmd_write_to_file
  end interface

  interface x1_at_node
    module procedure x1_node_discrete_1d, x1_node_analytic_1d
  end interface

  interface x1_at_cell
    module procedure x1_cell_discrete_1d, x1_cell_analytic_1d
  end interface

  interface jacobian_at_node
    module procedure mesh_1d_jacobian_node_discrete
  end interface

  interface jacobian_at_cell
    module procedure mesh_1d_jacobian_cell_discrete
  end interface

  interface jacobian
    module procedure jacobian_1d_discrete
  end interface
#else
  abstract interface
     function one_arg_message_passing_func_analyt( map, eta1 )
       use sll_working_precision
       import :: sll_mapped_mesh_1d_analytic
       sll_real64 :: one_arg_message_passing_func_analyt
       class(sll_mapped_mesh_1d_analytic)  :: map
       sll_real64, intent(in)              :: eta1
     end function one_arg_message_passing_func_analyt
  end interface


  abstract interface
     function one_arg_message_passing_func_discr( map, eta1 )
       use sll_working_precision
       import     :: sll_mapped_mesh_1d_discrete
       sll_real64                          :: one_arg_message_passing_func_discr
       class(sll_mapped_mesh_1d_discrete)  :: map
       sll_real64, intent(in)              :: eta1
     end function one_arg_message_passing_func_discr
  end interface
#endif 

contains

 !**************************************************************************
  !
  !               Functions for the analytic general map
  !
  !**************************************************************************

  ! initialize_mapped_mesh_1d_general() allocates all the memory needed by 
  ! the 1D map. 
  !

  subroutine initialize_mesh_1d_analytic( &
    mesh,           &
    label,          &
    npts,           &
    x1_func,        &
    jacobian_func )

#ifdef STDF95
    type(sll_mapped_mesh_1d_analytic), intent(inout)  :: mesh
#else
    class(sll_mapped_mesh_1d_analytic), intent(inout) :: mesh
#endif
    character(len=*), intent(in)                  :: label
    sll_int32, intent(in)                         :: npts

#ifdef STDF95
    sll_real64                                    :: x1_func
    sll_real64                                    :: jacobian_func
#else
    procedure(one_arg_scalar_function)            :: x1_func
    procedure(one_arg_scalar_function)            :: jacobian_func
#endif

    sll_real64 :: delta_1  ! cell spacing in eta1 
    sll_real64 :: eta_1
    sll_real64 :: jacobian_val
    sll_int32  :: i
    sll_int32  :: ierr
    sll_int32  :: npts1

    npts1 = npts
    mesh%label      = trim(label)
    mesh%nc_eta1    = npts1-1
    delta_1         = 1.0_f64/(npts1 - 1)
    mesh%delta_eta1 = delta_1

    ! Allocate the arrays for precomputed jacobians.
    SLL_ALLOCATE(mesh%jacobians_n(npts1), ierr)
    SLL_ALLOCATE(mesh%jacobians_c(npts1-1), ierr)

    ! Allocation for x1 at nodes, needed regardless of the type of map
    SLL_ALLOCATE(mesh%x1_node(npts1), ierr)

    ! Start filling out the fields and allocating the object's memory.
    SLL_ALLOCATE(mesh%x1_cell(npts1-1), ierr)

    ! Assign the transformation functions
#ifdef STDF95
#else
    mesh%x1_func       => x1_func
    mesh%jacobian_func => jacobian_func
#endif
    
    ! Allocate the arrays for precomputed jacobians.
    SLL_ALLOCATE(mesh%jacobians_n(npts1), ierr)
    SLL_ALLOCATE(mesh%jacobians_c(npts1-1), ierr)
    
    ! Fill the values of the transformation and the jacobians at the nodes
    do i=0, npts1 - 1
       eta_1 = real(i,f64)*delta_1
       mesh%x1_node(i+1) = x1_func(eta_1)
       ! for some compiler reason, the following intermediate 
       ! variable is required, else the jacobians_n array will not
       ! be filled out properly.
       jacobian_val          = jacobian_func(eta_1)
       mesh%jacobians_n(i+1) = jacobian_val
    end do
    
    ! Fill the values at the mid-point of the cells
    do i=0, npts1 - 2
       eta_1 = delta_1*(real(i,f64) + 0.5_f64)
       mesh%x1_cell(i+1)     = x1_func(eta_1)
       mesh%jacobians_c(i+1) = jacobian_func(eta_1)
    end do
  end subroutine initialize_mesh_1d_analytic

  function x1_node_analytic_1d( mesh, i ) result(val)
#ifdef STDF95
    type(sll_mapped_mesh_1d_analytic) :: mesh
#else
    class(sll_mapped_mesh_1d_analytic) :: mesh
#endif
    sll_real64             :: val
    sll_int32, intent(in) :: i
    val = mesh%x1_node(i)
  end function x1_node_analytic_1d

  function x1_cell_analytic_1d( mesh, i ) result(val)
#ifdef STDF95
    type(sll_mapped_mesh_1d_analytic) :: mesh
#else
    class(sll_mapped_mesh_1d_analytic) :: mesh
#endif
    sll_real64             :: val
    sll_int32, intent(in) :: i
    val = mesh%x1_cell(i)
  end function x1_cell_analytic_1d

#ifdef STDF95
#else
  function x1_analytic_1d( mesh, eta1 ) result(val)
    class(sll_mapped_mesh_1d_analytic) :: mesh
    sll_real64                         :: val
    sll_real64, intent(in) :: eta1
    val = mesh%x1_func(eta1)
  end function x1_analytic_1d
#endif

  function mesh_1d_jacobian_node_analytic( mesh, i ) result(val)
#ifdef STDF95
    type(sll_mapped_mesh_1d_analytic)    :: mesh
#else
    class(sll_mapped_mesh_1d_analytic)   :: mesh
#endif
    sll_real64              :: val
    sll_int32, intent(in)   :: i
    sll_int32 :: num_pts_1
    num_pts_1 = mesh%nc_eta1 + 1
    SLL_ASSERT( (i .ge. 1) .and. (i .le. num_pts_1) )
    val = mesh%jacobians_n(i)
  end function mesh_1d_jacobian_node_analytic

  function mesh_1d_jacobian_cell_analytic( mesh, i ) result(val)
#ifdef STDF95
    type(sll_mapped_mesh_1d_analytic)    :: mesh
#else
    class(sll_mapped_mesh_1d_analytic)   :: mesh
#endif
    sll_real64              :: val
    sll_int32, intent(in)   :: i
    sll_int32 :: num_pts_1
    num_pts_1 = mesh%nc_eta1 
    SLL_ASSERT( (i .ge. 1) .and. (i .le. num_pts_1) )
    val = mesh%jacobians_c(i)
  end function mesh_1d_jacobian_cell_analytic

#ifdef STDF95
#else
  function jacobian_1d_analytic( mesh, eta1 ) result(val)
    sll_real64                         :: val
    class(sll_mapped_mesh_1d_analytic) :: mesh
    sll_real64, intent(in) :: eta1
    val = mesh%jacobian_func(eta1)
  end function jacobian_1d_analytic
#endif

#ifdef STDF95
  subroutine mma_write_to_file(mesh,output_format)
    type(sll_mapped_mesh_1d_analytic)  :: mesh
    sll_int32, optional            :: output_format
    sll_int32                      :: error
    sll_real64, dimension(:), allocatable  :: x1_array
    sll_int32                      :: num_pts1  
    sll_int32                      :: i

    if(present(output_format))then
      print*,'There is just gnuplot format available'
    endif
    

    num_pts1 = mesh%nc_eta1+1
    SLL_ALLOCATE(x1_array(num_pts1),error)
    do i=1,num_pts1
       x1_array(i) = x1_at_node(mesh,i)
    end do
    call sll_gnuplot_write(x1_array,mesh%label,error)
    SLL_DEALLOCATE_ARRAY(x1_array,error)
  end subroutine
#endif

  ! Discrete case:

  subroutine initialize_mesh_1d_discrete( &
    mesh,            &
    label,            &
    npts1,          &
    x1_node,        &
    x1_interpolator, &
    jacobians_n_interpolator, &
    jacobians_node, &
    x1_cell, &
    jacobians_cell )

#ifdef STDF95
    type(sll_mapped_mesh_1d_discrete)     :: mesh
#else
    class(sll_mapped_mesh_1d_discrete)    :: mesh
#endif
    character(len=*), intent(in)          :: label
    sll_int32, intent(in)                 :: npts1
    sll_real64, dimension(:)              :: x1_node
#ifdef STDF95
    type(cubic_spline_1d_interpolator), target  :: x1_interpolator
    type(cubic_spline_1d_interpolator), target  :: jacobians_n_interpolator
#else
    class(sll_interpolator_1d_base), target  :: x1_interpolator
    class(sll_interpolator_1d_base), target  :: jacobians_n_interpolator
#endif
    sll_real64, dimension(:), optional :: jacobians_node
    sll_real64, dimension(:), optional :: jacobians_cell
    sll_real64, dimension(:), optional :: x1_cell

    sll_real64 :: eta_1
    sll_real64 :: jacobian_val
    sll_int32  :: i
    sll_int32  :: ierr
    logical    :: x1c
    logical    :: jc
    logical    :: jn

    mesh%label = trim(label)
    x1c = present(x1_cell)
    jc  = present(jacobians_cell)
    jn  = present(jacobians_node)
    ! Samuel : I've commented the next line because jacobian_func isn't use
    !mesh%jacobian_func => null() !This functions is never initialize!!

    ! Check argument consistency
    ! DISCRETE_MAPs require only some of the parameters. If the mapping is
    ! defined from the nodes of the logical (eta1) mesh to the nodes
    ! of the physical mesh (x1), then the node arrays are required:
    ! jacobians_node and x1_node.
    !
    ! If the transformation is done on the points at the center of the cells
    ! then these parameters are also required: 
    ! jacobians_cell and x1_cell.
    ! node and cell values are not mutually exclusive, thus all parameters 
    ! can be provided in the DISCRETE case. It is up to the caller to make
    ! sure that the data set is consistent.

    ! 1. Check that the discrete representation of the transformation is
    !    consistent with the size of the 2D array.
    if( (size(x1_node,1) .lt. npts1) ) then
       print *, 'ERROR, initialize_mapped_mesh_1d_discrete(), ', &
            'the size of the x1_node array is ', &
            'inconsistent with the number of points declared, npts1.'
       STOP
    end if
    if( jn .eqv. .true. ) then
       if( (size(jacobians_node,1) .lt. npts1 - 1 ) ) then
          print *, 'ERROR, initialize_mapped_mesh_1d_discrete(): ', &
               'the size of the jacobians_node array is ', &
               'inconsistent with the number of points declared, npts1.'
          STOP
       end if
    end if
    if( jc .eqv. .true. ) then
       if( (size(jacobians_cell,1) .lt. npts1 - 1 ) ) then
          print *, 'ERROR, initialize_mapped_mesh_1d_discrete(): ', &
               'the size of the jacobians_cell arrays is ', &
               'inconsistent with the number of points declared, npts1.'
          STOP
       end if
    end if

    mesh%nc_eta1    = npts1-1
    mesh%delta_eta1 = 1.0_f64/(npts1 - 1)
    mesh%x1_interp  => x1_interpolator

    ! Allocate the arrays for precomputed jacobians.
    SLL_ALLOCATE(mesh%jacobians_n(npts1), ierr)
    SLL_ALLOCATE(mesh%jacobians_c(npts1-1), ierr)

    ! Allocation for x1 at nodes
    SLL_ALLOCATE(mesh%x1_node(npts1), ierr)

    ! Start filling out the fields and allocating the object's memory.
    SLL_ALLOCATE(mesh%x1_node(npts1), ierr)

    ! initialize the local arrays. Note that since the map has its
    ! own copies, it owns this information locally and will destroy
    ! this information when the object is deleted. The caller is
    ! thus responsible for deallocating the arrays that were passed as
    ! arguments.
    do i=1, npts1
       mesh%x1_node(i) = x1_node(i)
    end do
    
    ! Compute the spline coefficients
#ifdef STDF95 
    call cubic_spline_compute_interpolants( x1_interpolator , mesh%x1_node )
#else
    call x1_interpolator%compute_interpolants( mesh%x1_node )
#endif

    ! The splines contain all the information to compute the
    ! jacobians everywhere; however, here we explore assigning
    ! the jacobians-at-the-nodes array with the values provided
    ! by the user if available. If there are discrepancies between
    ! the user-provided values and the predictions from the splines,
    ! then this may itself be a way to look for errors.
    !
    ! Copy the values of the jacobians at the nodes if user given:
    if( jn .eqv. .true. ) then
       do i=1, npts1
          mesh%jacobians_n(i) = jacobians_node(i)
       end do
    else
       ! Fill the jacobian values at the nodes calculated from the splines
       do i=0, npts1 - 1
          eta_1 = real(i,f64)*mesh%delta_eta1
          ! FIX THIS PART!!!
          ! Samuel : I replace the line
          !jacobian_val = mesh%jacobian_func(eta_1)
          ! by the next
#ifdef STDF95
          jacobian_val = jacobian(mesh,eta_1)
#else
          jacobian_val = mesh%jacobian(eta_1)
#endif
          mesh%jacobians_n(i+1) = jacobian_val
       end do
    end if

    ! copy the cell-based transformation arrays if available
    if( x1c .eqv. .true. ) then
       SLL_ALLOCATE(mesh%x1_cell(npts1-1), ierr)
       do i=1, npts1 - 1
          mesh%x1_cell(i) = x1_cell(i)
       end do
    end if
    ! copy the cell-based jacobians if available
    if( jc .eqv. .true. ) then
       do i=1, npts1 - 1
          mesh%jacobians_c(i) = jacobians_cell(i)
       end do
    end if
  end subroutine initialize_mesh_1d_discrete

  function x1_node_discrete_1d( mesh, i ) result(val)
#ifdef STDF95
    type(sll_mapped_mesh_1d_discrete) :: mesh
#else
    class(sll_mapped_mesh_1d_discrete) :: mesh
#endif
    sll_real64             :: val
    sll_int32, intent(in) :: i
    val = mesh%x1_node(i)
  end function x1_node_discrete_1d

  function x1_cell_discrete_1d( mesh, i ) result(val)
#ifdef STDF95
    type(sll_mapped_mesh_1d_discrete) :: mesh
#else
    class(sll_mapped_mesh_1d_discrete) :: mesh
#endif
    sll_real64             :: val
    sll_int32, intent(in) :: i
    val = mesh%x1_cell(i)
  end function x1_cell_discrete_1d

  function mesh_1d_jacobian_node_discrete( mesh, i ) result(var)
#ifdef STDF95
    type(sll_mapped_mesh_1d_discrete)   :: mesh
#else
    class(sll_mapped_mesh_1d_discrete)   :: mesh
#endif
    sll_real64              :: var
    sll_int32, intent(in)   :: i
    sll_int32 :: num_pts_1
    num_pts_1 = mesh%nc_eta1 + 1
    SLL_ASSERT( (i .ge. 1) .and. (i .le. num_pts_1) )
    var = mesh%jacobians_n(i)
  end function mesh_1d_jacobian_node_discrete

  function mesh_1d_jacobian_cell_discrete( mesh, i ) result(var)
#ifdef STDF95
    type(sll_mapped_mesh_1d_discrete)   :: mesh
#else
    class(sll_mapped_mesh_1d_discrete)   :: mesh
#endif
    sll_real64              :: var
    sll_int32, intent(in)   :: i
    sll_int32 :: num_pts_1
    num_pts_1 = mesh%nc_eta1
    SLL_ASSERT( (i .ge. 1) .and. (i .le. num_pts_1) )
    var = mesh%jacobians_c(i)
  end function mesh_1d_jacobian_cell_discrete

  function jacobian_1d_discrete( mesh, eta1 ) result(jac)
#ifdef STDF95
    type(sll_mapped_mesh_1d_discrete) :: mesh
#else
    class(sll_mapped_mesh_1d_discrete) :: mesh
#endif
    sll_real64             :: jac
    sll_real64, intent(in) :: eta1
#ifdef STDF95
    jac = cubic_spline_interpolate_derivative_eta1( mesh%x1_interp , eta1 )
#else
    jac = mesh%x1_interp%interpolate_derivative_eta1( eta1 )
#endif
  end function jacobian_1d_discrete

  function x1_discrete_1d( mesh, eta1 ) result(val)
#ifdef STDF95
    type(sll_mapped_mesh_1d_discrete) :: mesh
#else
    class(sll_mapped_mesh_1d_discrete) :: mesh
#endif
    sll_real64                         :: val
    sll_real64, intent(in) :: eta1
#ifdef STDF95
    val = cubic_spline_interpolate_value(mesh%x1_interp,eta1)
#else
    val = mesh%x1_interp%interpolate_value(eta1)
#endif
  end function x1_discrete_1d

#ifdef STDF95
  subroutine mmd_write_to_file(mesh,output_format)
    type(sll_mapped_mesh_1d_discrete)  :: mesh
    sll_int32, optional            :: output_format
    sll_int32                      :: error
    sll_real64, dimension(:), allocatable  :: x1_array
    sll_int32                      :: num_pts1  
    sll_int32                      :: i

    if(present(output_format))then
      print*,'There is just gnuplot format available'
    endif
    

    num_pts1 = mesh%nc_eta1+1
    SLL_ALLOCATE(x1_array(num_pts1),error)
    do i=1,num_pts1
       x1_array(i) = x1_node_discrete_1d(mesh,i)
    end do
    call sll_gnuplot_write(x1_array,mesh%label,error)
    SLL_DEALLOCATE_ARRAY(x1_array,error)
  end subroutine
#endif

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

end module sll_module_mapped_meshes_1d
