!**************************************************************
!  Copyright INRIA
!  Authors : 
!     CALVI project team
!  
!  This code SeLaLib (for Semi-Lagrangian-Library) 
!  is a parallel library for simulating the plasma turbulence 
!  in a tokamak.
!  
!  This software is governed by the CeCILL-B license 
!  under French law and abiding by the rules of distribution 
!  of free software.  You can  use, modify and redistribute 
!  the software under the terms of the CeCILL-B license as 
!  circulated by CEA, CNRS and INRIA at the following URL
!  "http://www.cecill.info". 
!**************************************************************

! in development; should be at least cubic splines
! attached with computation of characteristics


module sll_m_advection_2d_bsl
#include "sll_working_precision.h"
#include "sll_memory.h"
#include "sll_assert.h"
use sll_m_boundary_condition_descriptors
use sll_m_advection_2d_base
use sll_m_characteristics_2d_base
use sll_m_interpolators_2d_base
implicit none

  type,extends(sll_advection_2d_base) :: BSL_2d_advector
  
    class(sll_c_interpolator_2d), pointer  :: interp
    class(sll_characteristics_2d_base), pointer  :: charac
    sll_real64, dimension(:), pointer :: eta1_coords
    sll_real64, dimension(:), pointer :: eta2_coords
    sll_real64, dimension(:,:), pointer :: charac_feet1
    sll_real64, dimension(:,:), pointer :: charac_feet2
    sll_int32 :: Npts1
    sll_int32 :: Npts2  
  contains
     procedure, pass(adv) :: initialize => &
       initialize_BSL_2d_advector
    procedure, pass(adv) :: advect_2d => &
      BSL_advect_2d
  
  end type BSL_2d_advector
   




contains
  function new_BSL_2d_advector( &
    interp, &
    charac, &
    Npts1, &
    Npts2, &
    eta1_min, &
    eta1_max, &
    eta2_min, &
    eta2_max, &
    eta1_coords, &
    eta2_coords) &  
    result(adv)      
    type(BSL_2d_advector), pointer :: adv
    class(sll_c_interpolator_2d), pointer :: interp
    class(sll_characteristics_2d_base), pointer  :: charac
    sll_int32, intent(in) :: Npts1
    sll_int32, intent(in) :: Npts2
    sll_real64, intent(in), optional :: eta1_min
    sll_real64, intent(in), optional :: eta1_max
    sll_real64, intent(in), optional :: eta2_min
    sll_real64, intent(in), optional :: eta2_max
    sll_real64, dimension(:), pointer, optional :: eta1_coords
    sll_real64, dimension(:), pointer, optional :: eta2_coords
    sll_int32 :: ierr
    
    SLL_ALLOCATE(adv,ierr)
        
    call initialize_BSL_2d_advector(&
      adv, &
      interp, &
      charac, &
      Npts1, &
      Npts2, &
      eta1_min, &
      eta1_max, &
      eta2_min, &
      eta2_max, &
      eta1_coords, &
      eta2_coords)    
    
  end function  new_BSL_2d_advector


  subroutine initialize_BSL_2d_advector(&
    adv, &
    interp, &
    charac, &
    Npts1, &
    Npts2, &
    eta1_min, &
    eta1_max, &
    eta2_min, &
    eta2_max, &
    eta1_coords, &
    eta2_coords)    
    class(BSL_2d_advector), intent(inout) :: adv
    class(sll_c_interpolator_2d), pointer :: interp
    class(sll_characteristics_2d_base), pointer  :: charac
    sll_int32, intent(in) :: Npts1
    sll_int32, intent(in) :: Npts2
    sll_real64, intent(in), optional :: eta1_min
    sll_real64, intent(in), optional :: eta1_max
    sll_real64, intent(in), optional :: eta2_min
    sll_real64, intent(in), optional :: eta2_max
    sll_real64, dimension(:), pointer, optional :: eta1_coords
    sll_real64, dimension(:), pointer, optional :: eta2_coords
    sll_int32 :: ierr
    sll_int32 :: i
    sll_real64 :: delta_eta1
    sll_real64 :: delta_eta2
    
    
    adv%Npts1 = Npts1
    adv%Npts2 = Npts2
    adv%interp => interp
    adv%charac => charac
    !SLL_ALLOCATE(adv%x1_mesh(Npts1),ierr)
    !SLL_ALLOCATE(adv%x2_mesh(Npts2),ierr)
    SLL_ALLOCATE(adv%eta1_coords(Npts1),ierr)
    SLL_ALLOCATE(adv%eta2_coords(Npts2),ierr)

    SLL_ALLOCATE(adv%charac_feet1(Npts1,Npts2),ierr)
    SLL_ALLOCATE(adv%charac_feet2(Npts1,Npts2),ierr)

    if(present(eta1_min).and.present(eta1_max))then
      if(present(eta1_coords))then
        print *,'#provide either eta1_coords or eta1_min and eta1_max'
        print *,'#and not both in subroutine initialize_BSL_2d_advector'
        stop
      else
        delta_eta1 = (eta1_max-eta1_min)/real(Npts1-1,f64)
        do i=1,Npts1
          adv%eta1_coords(i) = eta1_min+real(i-1,f64)*delta_eta1
        enddo
      endif
    else if(present(eta1_coords))then
      if(size(eta1_coords,1)<Npts1)then
        print *,'#bad size for eta1_coords in initialize_BSL_2d_advector'
        stop
      else
        adv%eta1_coords(1:Npts1) = eta1_coords(1:Npts1)
      endif     
    else
      print *,'#Warning, we assume eta1_min = 0._f64 eta1_max = 1._f64'
      delta_eta1 = 1._f64/real(Npts1-1,f64)
      do i=1,Npts1
          adv%eta1_coords(i) = real(i-1,f64)*delta_eta1
      enddo                      
    endif


    if(present(eta2_min).and.present(eta2_max))then
      if(present(eta2_coords))then
        print *,'#provide either eta2_coords or eta2_min and eta2_max'
        print *,'#and not both in subroutine initialize_BSL_2d_advector'
        stop
      else
        delta_eta2 = (eta2_max-eta2_min)/real(Npts2-1,f64)
        do i=1,Npts2
          adv%eta2_coords(i) = eta2_min+real(i-1,f64)*delta_eta2
        enddo
      endif
    else if(present(eta2_coords))then
      if(size(eta2_coords,1)<Npts2)then
        print *,'#bad size for eta2_coords in initialize_BSL_2d_advector'
        stop
      else
        adv%eta2_coords(1:Npts2) = eta2_coords(1:Npts2)
      endif     
    else
      print *,'#Warning, we assume eta2_min = 0._f64 eta2_max = 1._f64'
      delta_eta2 = 1._f64/real(Npts2-1,f64)
      do i=1,Npts2
          adv%eta2_coords(i) = real(i-1,f64)*delta_eta2
      enddo                      
    endif
    
      
  end subroutine initialize_BSL_2d_advector

  subroutine BSL_advect_2d(&
    adv, &
    A1, &
    A2, &
    dt, &
    input, &
    output)
    class(BSL_2d_advector) :: adv
    sll_real64, dimension(:,:), intent(in) :: A1
    sll_real64, dimension(:,:), intent(in) :: A2
    sll_real64, intent(in) :: dt 
    sll_real64, dimension(:,:), intent(in) :: input
    sll_real64, dimension(:,:), intent(out) :: output      
    
    call adv%charac%compute_characteristics( &
      A1, &
      A2, &
      dt, &
      adv%eta1_coords, &
      adv%eta2_coords, &
      adv%charac_feet1, &
      adv%charac_feet2)
    
    
!    call adv%interp%compute_interpolants( &
!      input, &
!      adv%eta1_coords, &
!      adv%Npts1, &
!      adv%eta2_coords, &
!      adv%Npts2 )

    call adv%interp%interpolate_array( &
      adv%Npts1, &
      adv%Npts2, &
      input, &
      adv%charac_feet1, &
      adv%charac_feet2, &
      output)      
          
  end subroutine BSL_advect_2d





end module sll_m_advection_2d_bsl
