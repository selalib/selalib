program test_poisson_2d_periodic_cart_par
!+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++
#include "sll_assert.h"
#include "sll_memory.h"
#include "sll_working_precision.h"

  use hdf5, only: &
    hid_t, &
    hsize_t

  use iso_fortran_env, only: &
    output_unit

  use sll_m_collective, only: &
    sll_boot_collective, &
    sll_collective_reduce, &
    sll_collective_t, &
    sll_get_collective_rank, &
    sll_get_collective_size, &
    sll_halt_collective, &
    sll_world_collective

  use sll_m_constants, only: &
    sll_pi

  use sll_m_gnuplot_parallel, only: &
    sll_gnuplot_rect_2d_parallel

  use sll_m_hdf5_io_parallel, only: &
    sll_hdf5_file_create, &
    sll_hdf5_write_array

  use sll_m_hdf5_io_serial, only: &
    sll_hdf5_file_close

  use sll_m_poisson_2d_periodic_cartesian_par, only: &
    delete_poisson_2d_periodic_plan_cartesian_par, &
    new_poisson_2d_periodic_plan_cartesian_par, &
    new_poisson_2d_periodic_plan_cartesian_par_alt, &
    poisson_2d_periodic_plan_cartesian_par, &
    solve_poisson_2d_periodic_cartesian_par, &
    solve_poisson_2d_periodic_cartesian_par_alt

  use sll_m_remapper, only: &
    compute_local_sizes, &
    get_layout_collective, &
    get_layout_i_min, &
    get_layout_j_min, &
    initialize_layout_with_distributed_array, &
    layout_2d, &
    local_to_global, &
    new_layout_2d, &
    sll_view_lims

  use sll_mpi, only: &
    mpi_prod

  implicit none
!+++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++++

  type (poisson_2d_periodic_plan_cartesian_par), pointer :: plan
  type (poisson_2d_periodic_plan_cartesian_par), pointer :: plan_alt

  sll_int32                               :: ncx, ncy
  sll_int32                               :: nx_loc, ny_loc
  sll_int32                               :: error
  sll_real64                              :: Lx, Ly
  sll_real64                              :: dx, dy
  sll_real64                              :: x, y
  sll_real64, dimension(:,:), allocatable :: rho
  sll_real64, dimension(:,:), allocatable :: phi_an
  sll_real64, dimension(:,:), allocatable :: phi
  sll_int32                               :: i, j
  sll_real64                              :: average_err
  sll_int32, dimension(1:2)               :: global
  sll_int32                               :: gi, gj
  sll_int32                               :: myrank
  type(layout_2D), pointer                :: layout_x
  type(layout_2D), pointer                :: layout_alt
  sll_int64                               :: colsz ! collective size
  sll_int32                               :: nprocx, nprocy
  sll_int32                               :: e
  sll_real32                              :: ok 
  sll_real32, dimension(1)                :: prod4test
  sll_int32                               :: offset(2)

  ok = 1.0

  !Boot parallel environment
  call sll_boot_collective()

  ! Number of cells is equal to number of points in this case
  ncx = 512
  ncy = 512
  Lx  = 2.0*sll_pi
  Ly  = 2.0*sll_pi

  colsz  = int(sll_get_collective_size(sll_world_collective),i64)
  myrank = sll_get_collective_rank(sll_world_collective)

  dx = Lx/ncx
  dy = Ly/ncy

  colsz  = int(sll_get_collective_size(sll_world_collective),i64)
  e      = int(log(real(colsz))/log(2.))
  print *, 'running on ', 2**e, 'processes'

!########### ALTERNATIVE SOLVER ##########################################

  ! Layout and local sizes for FFTs in x-direction
  layout_alt => new_layout_2D( sll_world_collective )
  nprocx = 1
  nprocy = 2**e
  call initialize_layout_with_distributed_array( ncx, ncy, &
       nprocx, nprocy, layout_alt )

  plan_alt => new_poisson_2d_periodic_plan_cartesian_par_alt(&
       layout_alt, ncx, ncy, Lx, Ly)

  call compute_local_sizes( layout_alt, nx_loc, ny_loc )
  call sll_view_lims( layout_alt )

  SLL_ALLOCATE(rho(nx_loc,ny_loc), error)
  SLL_ALLOCATE(phi_an(nx_loc,ny_loc), error)
  SLL_ALLOCATE(phi(nx_loc,ny_loc), error)

  ! initialize reference array
  do j=1,ny_loc
     do i=1,nx_loc
        global = local_to_global( layout_alt, (/i, j/))
        gi = global(1)
        gj = global(2)
        x  = (gi-1)*dx
        y  = (gj-1)*dy
        phi_an(i,j) =  cos(x)*sin(y) 
        rho(i,j)    = -2.0_f64*phi_an(i,j)
     end do
  end do

  call parallel_hdf5_write_array_2d( 'q_density.h5', &
     ncx, ncy, rho,  'rho', layout_alt)

  call solve_poisson_2d_periodic_cartesian_par_alt(plan_alt, rho, phi)

  call parallel_hdf5_write_array_2d( 'phi_analytical.h5', &
     ncx, ncy, phi_an, 'phi_an', layout_alt)
  call parallel_hdf5_write_array_2d( 'phi_computed.h5',   &
     ncx, ncy, phi, 'phi', layout_alt)

  average_err  = sum(abs(phi_an-phi))/real(ncx*ncy,f64)

  flush( output_unit ); print*, ' ------------------'
  flush( output_unit ); print*, ' myrank ', myrank
  flush( output_unit ); print*, 'local average error:', average_err
  flush( output_unit ); print*, 'dx*dy =', dx*dy
  flush( output_unit ); print*, ' ------------------'

  if (average_err> 1.0e-06 ) then
     print*, 'Test stopped by "sll_poisson_2d_periodic_par" failure'
     !call sll_halt_collective()
     !stop
  endif
 
  SLL_DEALLOCATE_ARRAY(phi,    error)
  SLL_DEALLOCATE_ARRAY(rho,    error)
  SLL_DEALLOCATE_ARRAY(phi_an, error)

  call delete_poisson_2d_periodic_plan_cartesian_par(plan_alt)

!#########FIRST VERSION WITH LAST PERIODIC POINT ADDED #########################

  ! Layout and local sizes for FFTs in x-direction
  layout_x => new_layout_2D( sll_world_collective )
  nprocx = 1
  nprocy = 2**e
  call initialize_layout_with_distributed_array( ncx+1, ncy+1, &
       nprocx, nprocy, layout_x )

  plan => new_poisson_2d_periodic_plan_cartesian_par(&
       layout_x, ncx, ncy, Lx, Ly)

  call compute_local_sizes( layout_x, nx_loc, ny_loc )
  call sll_view_lims( layout_x )

  SLL_ALLOCATE(rho(nx_loc,ny_loc), error)
  SLL_ALLOCATE(phi_an(nx_loc,ny_loc), error)
  SLL_ALLOCATE(phi(nx_loc,ny_loc), error)

  ! initialize reference array
  do j=1,ny_loc
     do i=1,nx_loc
        global = local_to_global( layout_x, (/i, j/))
        gi = global(1)
        gj = global(2)
        x  = (gi-1)*dx
        y  = (gj-1)*dy
        phi_an(i,j) =  cos(x)*sin(y) 
        rho(i,j)    = -2.0_f64*phi_an(i,j)
     end do
  end do

  call solve_poisson_2d_periodic_cartesian_par(plan, rho, phi)

  offset(1) =  get_layout_i_min( layout_x, myrank ) - 1
  offset(2) =  get_layout_j_min( layout_x, myrank ) - 1
  call sll_gnuplot_rect_2d_parallel(dble(offset(1)), dble(1), &
                                    dble(offset(2)), dble(1), &
                                    size(rho,1), size(rho,2), &
                                    rho, "rho", 1, error)  

  average_err  = sum(abs(phi_an-phi))/real(ncx*ncy,f64)

  flush( output_unit ); print*, ' ------------------'
  flush( output_unit ); print*, ' myrank ', myrank
  flush( output_unit ); print*, 'local average error:', average_err
  flush( output_unit ); print*, 'dx*dy =', dx*dy
  flush( output_unit ); print*, ' ------------------'

  if (average_err> 1.0e-06 ) then
     print*, 'Test stopped by "sll_poisson_2d_periodic_par" failure'
     call sll_halt_collective()
     stop
  endif
 
  SLL_DEALLOCATE_ARRAY(phi,    error)
  SLL_DEALLOCATE_ARRAY(rho,    error)
  SLL_DEALLOCATE_ARRAY(phi_an, error)

  call sll_collective_reduce(sll_world_collective, (/ ok /), &
       1, MPI_PROD, 0, prod4test )

  if (myrank==0) then
     if (prod4test(1)==1.) then
        flush( output_unit )
        print*, ' '
        flush( output_unit )
        print*, '"sll_poisson_2d_periodic_cart_par" test: PASSED'
        flush( output_unit )
        print*, ' '
     endif
  endif

  call delete_poisson_2d_periodic_plan_cartesian_par(plan)

  call sll_halt_collective()

contains

  ! Experimental interface for an hdf5 array writer in parallel
  subroutine parallel_hdf5_write_array_2d( &
    filename, &
    n_pts1, &
    n_pts2, &
    array, &
    dataset_name, &
    layout )

    character(len=*), intent(in)           :: filename
    sll_int32, intent(in)                  :: n_pts1
    sll_int32, intent(in)                  :: n_pts2
    integer(HSIZE_T), dimension(1:2)       :: global_dims
    sll_real64, dimension(:,:), intent(in) :: array
    character(len=*), intent(in)           :: dataset_name
    type(layout_2D), pointer               :: layout
    sll_int32                              :: error
    integer(HID_T)                         :: file_id
    integer(HSIZE_T), dimension(1:2)       :: offset
    type(sll_collective_t), pointer        :: col
    sll_int32                              :: myrank
    sll_int32                              :: comm

    SLL_ASSERT( associated(layout) )
    col => get_layout_collective( layout )
    myrank = sll_get_collective_rank( col )
    global_dims(:) = [int(n_pts1,HSIZE_T),int(n_pts2,HSIZE_T)]
    
    offset(1) = int(get_layout_i_min( layout, myrank ) - 1, HSIZE_T)
    offset(2) = int(get_layout_j_min( layout, myrank ) - 1, HSIZE_T)

    comm   = sll_world_collective%comm
    call sll_hdf5_file_create(filename,comm,file_id,error)
    call sll_hdf5_write_array(file_id,global_dims,offset, &
                              array,dataset_name,error)
    call sll_hdf5_file_close(file_id,error)

  end subroutine parallel_hdf5_write_array_2d

end program test_poisson_2d_periodic_cart_par
